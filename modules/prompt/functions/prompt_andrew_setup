#
# A theme modified from the steeef theme
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Andrew Dunham <andrew@du.nham.ca>

function prompt_andrew_check_git {
  # Clear by default
  __andrew_git_extra=''

  local st="$(git status --ignore-submodules 2>/dev/null)"
  if [[ -n "$st" ]]; then
    if [[ ! $st =~ 'nothing to commit' ]]; then
      __andrew_git_extra='⚡'
    fi
  fi
}

function prompt_andrew_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Call extended information function.
  prompt_andrew_check_git

  # Get ruby/python information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_andrew_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_andrew_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_ANDREW_COLORS=(
      "%F{141}"         # turquoise
      "%F{172}"         # orange
      "%F{magenta}"     # purple
      "%F{161}"         # hotpink
      "%F{green}"       # limegreen
    )
  else
    __PROMPT_ANDREW_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  # turquoise / green / orange / hotpink / magenta
  #     echo "$(tput setaf 141)branch $(tput setaf 9)untracked"
  #     echo "$(tput setaf 2)added $(tput setaf 172)deleted $(tput setaf 161)modified"
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_ANDREW_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:ahead' format "%%B${__PROMPT_ANDREW_COLORS[1]}↑%f%%b"
  zstyle ':prezto:module:git:info:behind' format "%%B${__PROMPT_ANDREW_COLORS[1]}↓%f%%b"
  zstyle ':prezto:module:git:info:dirty' format "%%B${__PROMPT_ANDREW_COLORS[1]}*%%b"
  zstyle ':prezto:module:git:info:untracked' format "%%B%F{yellow}●%f%%b"

  # These don't do anything by default, since they must be enabled by:
  #     zstyle ':prezto:module:git:info' verbose 'yes'
  #zstyle ':prezto:module:git:info:added' format "${__PROMPT_ANDREW_COLORS[5]}●add%f"
  #zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_ANDREW_COLORS[2]}●del%f"
  #zstyle ':prezto:module:git:info:modified' format "${__PROMPT_ANDREW_COLORS[4]}●mod%f"

  # Actual prompt info
  zstyle ':prezto:module:git:info:keys' format 'prompt' 'on %b%A%B%u'

  # ruby info (rvm, rbenv)
  #zstyle ':prezto:module:ruby:info:version' format '[%v]'

  # Call our info function so the first status line is accurate.
  prompt_andrew_check_git

  PROMPT="${__PROMPT_ANDREW_COLORS[3]}%n%f at ${__PROMPT_ANDREW_COLORS[2]}%m%f in ${__PROMPT_ANDREW_COLORS[5]}%~%f "'$git_info[prompt]''${__andrew_git_extra}'"
$ "
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
  RPROMPT=''

  #RPROMPT='%F{blue}${ruby_info[version]}'
}

prompt_andrew_setup "$@"
